plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '2.26.0'
}

group = 'com.wakfoverlay'
version = '1.5.0'

java {
    sourceCompatibility = JavaVersion.VERSION_23
    targetCompatibility = JavaVersion.VERSION_23
}

repositories {
    mavenCentral()
}

javafx {
    version = '23'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    testImplementation("org.mockito:mockito-junit-jupiter:5.17.0")

    testImplementation("org.junit.jupiter:junit-jupiter:5.9.2")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    testImplementation 'org.testfx:testfx-core:4.0.16-alpha'
    testImplementation 'org.testfx:testfx-junit5:4.0.16-alpha'

    testImplementation 'org.testfx:openjfx-monocle:jdk-12.0.1+2'
}

application {
    mainClass = 'com.wakfoverlay.ui.OverlayApp'
}

test {
    useJUnitPlatform()
}

run {
    enableAssertions = true
}

jlink {
    launcher {
        name = 'WakfOverlay'
    }
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    jpackage {
        icon = "src/main/resources/${resolveIcon()}"
        installerType = findProperty('imageType') ?: resolveInstallerType()
        installerOptions = [
            '--win-per-user-install',
            '--win-dir-chooser',
            '--win-menu',
            '--win-shortcut'
        ]
    }
}

def resolveIcon() {
    def os = org.gradle.internal.os.OperatingSystem.current()
    if (os.isWindows()) return "icon.ico"
    if (os.isMacOsX()) return "icon.icns"
    return "icon.png"
}

def resolveInstallerType() {
    def os = org.gradle.internal.os.OperatingSystem.current()
    if (os.isWindows()) return "exe"
    if (os.isMacOsX()) return "dmg"
    return "deb"
}